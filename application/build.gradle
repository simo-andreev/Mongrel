apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-spring'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'idea'
apply plugin: "jacoco"

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

// Kotlin docs tool module-specific config
dokka {
    // Specifies locations of project source on the Web.
    // Dokka generates "source" links for each declaration.
    linkMapping {
        // Source directories
        dir = "src/main/kotlin/bg/o/sim/application"
        // URL showing where the source code can be accessed through the web browser
        url = "https://github.com/simo-andreev/Mongrel/tree/master/application/src/main/kotlin/bg/o/sim/"
        suffix = "#L" // Suffix which is used to append the line number to the URL. Use #L for GitHub
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    implementation('org.springframework.boot:spring-boot-starter-cache')
    implementation('org.springframework.boot:spring-boot-starter-data-mongodb')
    implementation('org.springframework.boot:spring-boot-starter-data-mongodb-reactive')
    implementation('org.springframework.boot:spring-boot-starter-data-rest')
//    implementation('org.springframework.boot:spring-boot-starter-security')
//    implementation('org.springframework.boot:spring-security-web')
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation('org.springframework.boot:spring-boot-starter-webflux')
    implementation('org.springframework.boot:spring-boot-starter-web')

    implementation('com.fasterxml.jackson.module:jackson-module-kotlin')

    implementation('org.springframework.session:spring-session-core')

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('io.projectreactor:reactor-test')
    testImplementation('org.springframework.restdocs:spring-restdocs-mockmvc')
    testImplementation('org.springframework.security:spring-security-test')

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.1.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.1.1"

    implementation project(':annotations')
    implementation project(':aprocessor')
    kapt project(':aprocessor')
}

test {
    useJUnitPlatform()
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
}

jacocoTestReport {
    reports {
        xml.setEnabled(true)
        html.setEnabled(false)
        csv.setEnabled(false)
    }
}

check.dependsOn jacocoTestReport
